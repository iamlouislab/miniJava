/*
   Grammaire attribuée pour construire l'arbre abstrait correspondant au langage Block
*/

package fr.n7.stl.block;

import java_cup.runtime.*;
import fr.n7.stl.block.Lexer;
import java.io.IOException;
import java.io.File;
import java.io.FileInputStream;
import java.util.*;


// Elements du métamodèle pour la construction de l'arbre abstrait
import fr.n7.stl.block.ast.*;
import fr.n7.stl.block.ast.expression.*;
import fr.n7.stl.block.ast.miniJava.*;
import fr.n7.stl.block.ast.expression.accessible.*;
import fr.n7.stl.block.ast.expression.allocation.*;
import fr.n7.stl.block.ast.expression.assignable.*;
import fr.n7.stl.block.ast.expression.value.*;
import fr.n7.stl.block.ast.instruction.*;

import java.io.PrintStream;
import fr.n7.stl.block.ast.instruction.declaration.*;
import fr.n7.stl.block.ast.scope.*;
import fr.n7.stl.block.ast.type.*;
import fr.n7.stl.block.ast.type.declaration.*;
import fr.n7.stl.tam.ast.Fragment;
import fr.n7.stl.tam.ast.Register;
import fr.n7.stl.tam.ast.impl.TAMFactoryImpl;
import fr.n7.stl.util.*;
import java.io.FileOutputStream;

/* Variables partagées dans les actions de l'analyseur syntaxique. */
parser code {:
	protected Lexer lexer;
	protected String name;
	public Parser(String _name) {
		this();
		this.name = _name;
	}
:}

/* Initialisation de l'analyseur lexical et des variables partagées. */
init with {:
  ComplexSymbolFactory f = new ComplexSymbolFactory();
  symbolFactory = f;
  File file = new File(this.name);
  FileInputStream fis = null;
  try {
    fis = new FileInputStream(file);
  } catch (IOException e) {
    e.printStackTrace();
  } 
  lexer = new Lexer(f,fis);
:};

/* Expression qui permet de progresser dans l'analyse lexicale. */
scan with {: return lexer.next_token(); :};

/* Terminaux qui seront transmis par l'analyseur lexical. */
/* Partie miniJava */
terminal			UL_Classe, UL_Herite;
terminal			UL_Public, UL_Prive, UL_Protege, UL_Definitif, UL_Abstrait, UL_De_Classe;
terminal			UL_Classe_Principale, UL_Methode_Principale;
terminal			UL_This, UL_Super;
/* Partie Block */
terminal			UL_Point_Virgule, UL_Virgule, UL_Point, UL_Deux_Points, UL_Point_Interrogation, UL_Point_Exclamation;
terminal			UL_Egal;
terminal 			UL_Plus, UL_Moins, UL_Asterisque, UL_Oblique, UL_Pour_Cent;
terminal			UL_Double_Barre, UL_Double_Esperluette;
terminal			UL_Parenthese_Ouvrante, UL_Parenthese_Fermante;
terminal			UL_Accolade_Ouvrante, UL_Accolade_Fermante;
terminal			UL_Crochet_Ouvrant, UL_Crochet_Fermant;
terminal			UL_Inferieur, UL_Superieur;
terminal			UL_Inferieur_Egal, UL_Superieur_Egal;
terminal			UL_Double_Egal, UL_Exclamation_Egal;
terminal			UL_Si, UL_Sinon, UL_Tant_Que, UL_Nouveau, UL_Afficher, UL_Retour;
terminal			UL_Type_Booleen, UL_Type_Caractere, UL_Type_Chaine, UL_Type_Entier, UL_Type_Flottant, UL_Type_Vide;
terminal String		UL_Identificateur, UL_Chaine, UL_Caractere;
terminal String		UL_Nombre_Entier, UL_Nombre_Flottant, UL_Nul, UL_Vrai, UL_Faux; // Valeur transmise par l'analyseur lexical

/* Non terminaux avec type renvoyé par actions sémantiques (RESULT = ...) */
non terminal List<Element>				Program;
/* début modification miniJava */
non terminal Block						Block; 
/* fin modification miniJava */
non terminal List<Instruction>			Instructions; 
non terminal Instruction				Instruction;
non terminal Instruction				Declaration;
non terminal List<Expression>			Expressions;
non terminal List<ParameterDeclaration>	Parameters;
non terminal Pair<String,PartialType>	Identifiant;
non terminal Type						Type, Atomique;
non terminal Expression 				Expression;
non terminal AssignableExpression		Affectable;
/* miniJava */
non terminal List<Element>				Elements;
non terminal MainDeclaration			Main;
non terminal ClassDeclaration			Class;
non terminal							SingleInherits, MultipleInherits;
non terminal List<ClassElement>			ClassElements;
non terminal ClassElement 				ClassElement;
non terminal AttributeDeclaration		Attribute;
non terminal Signature					Signature;
non terminal ConstructorDeclaration		Constructor;
non terminal MethodDeclaration			Method;
non terminal List<Instance> 			Instances;
non terminal Instance					Instance;
non terminal AccessRight				AccessRight;

/* Associativité et Priorité relative des opérateurs (du moins prioritaire au plus prioritaire) */
/* Opérateur le moins prioritiaire */
precedence nonassoc UL_Parenthese_Fermante;	// Résolution du conflit décaler/réduire sur conversion de type
precedence right UL_Egal;
precedence nonassoc UL_Point_Interrogation, UL_Deux_Points;
precedence left UL_Double_Barre;
precedence left UL_Double_Esperluette;
precedence nonassoc UL_Double_Egal, UL_Exclamation_Egal;
precedence nonassoc UL_Inferieur, UL_Inferieur_Egal, UL_Superieur, UL_Superieur_Egal;
precedence left UL_Plus, UL_Moins;
precedence left UL_Asterisque, UL_Oblique, UL_Pour_Cent;
precedence right UL_Nouveau;
precedence left UL_Point_Exclamation;
precedence left UL_Point; // , UL_Crochet_Ouvrant, UL_Parenthese_Ouvrante;
/* Opérateur le plus prioritaire */

/* Règles de grammaire attribuée pour la construction de l'arbre abstrait */
/* Program et Block sont des non terminaux */
/* UL_Identificateur est un terminal (Unité lexicale) */
/* Block est la variable utilisable dans l'action sémantique qui contient la valeur renvoyé par l'analyse du Block */
/* TODO */
Program ::= Elements:elements
			{: 				
				List<Fragment> codeFragmentList = new ArrayList<>();
				Fragment allCode = (new TAMFactoryImpl()).createFragment();
				
				SymbolTable tds = new SymbolTable();
				for (int i = elements.size() - 1; i >= 0; i--) {
					Element e = elements.get(i);
					System.out.println("element: " + e);
					if (!e.collect(tds)) {
						System.out.println("Error collect for element: " + e);
						break;
					}
					else {
						System.out.println("collect succeed!");
						if (!e.resolve(tds)) {
							System.out.println("Error resolve for element: " + e);
							break;
						}
						else {
							System.out.println("resolve succeed!");
						}
					}
				}

				for (int i = elements.size() - 1; i >= 0; i--) {
					Element e = elements.get(i);
					if (!e.checkType()) {
						System.out.println("Error checkType for element: " + e);
						break;
					}
					else {
						System.out.println("checkType succeed!");
						/* e.allocateMemory(Register.SB, 0); */
						/* Fragment code = e.getCode(new TAMFactoryImpl()); */
						/* codeFragmentList.add(code); */
					}
				}

				for (int i = codeFragmentList.size() - 1; i >= 0; i--) {
					allCode.append(codeFragmentList.get(i));
				}

				File file = new File(parser.name.replaceAll(".txt", "") + ".tam");
				PrintStream printer = null;
				try {
					printer = new PrintStream( new FileOutputStream(file) );
					printer.println( allCode );
				} catch (IOException e) {
					e.printStackTrace();
				}
				
			:}
		;

/* ajout miniJava : classes, interfaces, attributs, méthodes, constructeurs, etc */
Elements ::= Main:main
		{:
			List<Element> elements = new LinkedList<Element>();
			elements.add(main);
			RESULT = elements;
		:}
		| Class:c Elements:elements
		{:
			elements.add(c);
			RESULT = elements;
		:}
		| UL_Abstrait Class:c Elements:elements
		{:
			elements.add(c);
			RESULT = elements;
		:}
		;
		
Class ::= UL_Classe UL_Identificateur:name 
		UL_Accolade_Ouvrante ClassElements:classElements UL_Accolade_Fermante
		{:
			RESULT = new ClassDeclaration(name, classElements);
		:}
		;
		
Main ::= UL_Public UL_Classe UL_Classe_Principale
		UL_Accolade_Ouvrante 
			UL_Public UL_De_Classe UL_Type_Vide UL_Methode_Principale 
			UL_Parenthese_Ouvrante Parameters:parameters UL_Parenthese_Fermante Block:block UL_Accolade_Fermante
		{:
			RESULT = new MainDeclaration(parameters, block);
		:}
		;
		
SingleInherits ::=
		{:
		:}
		|
		UL_Herite Instance:instance
		{:
			RESULT = new SingleInheritsDeclaration(instance);
		:}
		;
		
MultipleInherits ::= 
		{:
		:}
		|
		UL_Herite Instances:instances
		{:
			RESULT = new MultipleInheritsDeclaration(instances);
		:}
		;

		
Instances ::= Instance:instance UL_Virgule Instances:instances
		{:
			instances.add(instance);
			RESULT = instances;
		:}
		|
		Instance:instance
		{:
			List<Instance> instances = new LinkedList<>();
			instances.add(instance);
			RESULT = instances;
		:}
		;
		
Instance ::= UL_Identificateur:name
		{:
			RESULT = new Instance( name );
		:}
		;
		
ClassElements ::= /* Lambda */
		{:
			List<ClassElement> elements = new LinkedList<ClassElement>();
			RESULT = elements;
		:}
		| AccessRight:accessRight ClassElement:classElement ClassElements:classElements
		{:
			classElement.setAccessRight(accessRight);
			classElements.add(classElement);
			RESULT = classElements;
		:}
		;
AccessRight ::=
		UL_Public
		{:
			RESULT = AccessRight.Public;
		:}
		| UL_Protege
		{:
			RESULT = AccessRight.Protected;
		:}
		| UL_Prive
		{:
			RESULT = AccessRight.Private;
		:}
		;


ClassElement ::=
		Attribute:attribute
		{:
			RESULT = attribute;
		:}
		|
		Method:method
		{:
			RESULT = method;
		:}
		|
		Constructor:constructor
		{:
			RESULT = constructor;
		:}
		;

Attribute ::=
		Type:type Identifiant:identifiant UL_Point_Virgule
		{:
			RESULT = new AttributeDeclaration(type, identifiant);
		:}
		|
		UL_De_Classe Type:type Identifiant:identifiant UL_Egal Expression:valeur UL_Point_Virgule
		{:
			RESULT = new AttributeDeclaration(type, identifiant, valeur, ElementNature.Static);
		:}
		|
		UL_De_Classe UL_Definitif Type:type Identifiant:identifiant UL_Egal Expression:valeur UL_Point_Virgule
		{:
			RESULT = new AttributeDeclaration(type, identifiant, valeur, ElementNature.StaticFinal);
		:}
		;
		
Method ::=
		Signature:entete Block:corps
		{:
			RESULT = new MethodDeclaration(entete, corps, ElementNature.None);
		:}
		| 
		UL_Definitif Signature:entete Block:corps
		{:
			RESULT = new MethodDeclaration(entete, corps, ElementNature.Final);
		:}
		| 
		UL_De_Classe Signature:entete Block:corps
		{:
			RESULT = new MethodDeclaration(entete, corps, ElementNature.Static);
		:}
		| 
		UL_De_Classe UL_Definitif Signature:entete Block:corps
		{:
			RESULT = new MethodDeclaration(entete, corps, ElementNature.StaticFinal);
		:}
		| 
		UL_Abstrait Signature:entete UL_Point_Virgule
		{:
			RESULT = new MethodDeclaration(entete, null, ElementNature.Abstract);
		:}
		;
		
Signature ::=
		Type:type Identifiant:identifiant UL_Parenthese_Ouvrante Parameters:parametres UL_Parenthese_Fermante
		{:
			RESULT = new Signature(type, identifiant, parametres);
		:}
		|
		Type:type Identifiant:identifiant UL_Parenthese_Ouvrante UL_Parenthese_Fermante
		{:
			RESULT = new Signature(type, identifiant, null);
		:}
		;
		
Constructor ::=
		UL_Identificateur:nom UL_Parenthese_Ouvrante Parameters:parametres UL_Parenthese_Fermante Block:block
		{:
			RESULT = new ConstructorDeclaration(nom, parametres, block);
		:}
		|
		UL_Identificateur:nom UL_Parenthese_Ouvrante UL_Parenthese_Fermante Block:block
		{:
			RESULT = new ConstructorDeclaration(nom, null, block);
		:}
		;

/* fin ajout miniJava */
Block ::= UL_Accolade_Ouvrante Instructions:instructions UL_Accolade_Fermante 
			{: 
				RESULT = new Block( instructions ); 
			:}
		;
		
Atomique ::= UL_Type_Booleen
			{: 
				RESULT = AtomicType.BooleanType; 
			:}
		|	UL_Type_Caractere
			{: 
				RESULT = AtomicType.CharacterType; 
			:}
		|	UL_Type_Chaine
			{: 
				RESULT = AtomicType.StringType; 
			:}
		|	UL_Type_Entier
			{: 
				RESULT = AtomicType.IntegerType; 
			:}
		|	UL_Type_Flottant
			{: 
				RESULT = AtomicType.FloatingType; 
			:}
		|	UL_Type_Vide
			{: 
				RESULT = AtomicType.VoidType; 
			:}
		;
		
Type ::= Atomique:atomique
			{: 
				RESULT = atomique; 
			:}
		| Instance:instance
			{: 
				RESULT = instance; 
			:}
		;

Instructions ::= Instructions:instructions Instruction:instruction
			{:
				instructions.add( instruction );
				RESULT = instructions; 
			:}
		|
			{: RESULT = new LinkedList<Instruction>(); :}
		;

Identifiant ::= UL_Identificateur:nom
				{: 
					RESULT = new Pair<String,PartialType>( nom, null ); 
				:}
			| Identifiant:identifiant UL_Crochet_Ouvrant UL_Crochet_Fermant 
				{:
					if (identifiant.getRight() == null) {
						identifiant.setRight( new PartialArrayType() );
					} else {
						identifiant.getRight().enrich(new PartialArrayType());
					}
					RESULT = identifiant;
				:}
			;
			
Parameters ::= Parameters:parameters UL_Virgule Type:type Identifiant:identifiant
				{:
					Type _type = identifiant.getRight();
					if (_type == null) {
						_type = type;
					} else {
						_type = ((PartialType)_type).complete( type );
 					}
					parameters.add( new ParameterDeclaration( identifiant.getLeft(), _type) );
					RESULT = parameters;
				:}
			| Type:type Identifiant:identifiant
				{:
					List<ParameterDeclaration> _parameters = new LinkedList<ParameterDeclaration>();
					Type _type = identifiant.getRight();
					if (_type == null) {
						_type = type;
					} else {
						_type = ((PartialType)_type).complete( type );
 					}
					_parameters.add( new ParameterDeclaration( identifiant.getLeft(), _type) );
					RESULT = _parameters;
				:}
			;
			
Declaration ::= Type:type Identifiant:identifiant UL_Egal Expression:valeur UL_Point_Virgule
				{:
					Type _type = identifiant.getRight();
					if (_type == null) {
						_type = type;
					} else {
						_type = ((PartialType)_type).complete( type );
 					}
					RESULT = new VariableDeclaration( identifiant.getLeft(), _type, valeur);
				:}
			;

Instruction ::= Declaration:declaration
				{: 
					RESULT = declaration; 
				:}
			| Affectable:affectable UL_Egal Expression:expression UL_Point_Virgule
				{:
					RESULT = new Assignment( affectable, expression);
				:}
			/* début ajout miniJava : appel de méthodes sans retour */
			| Affectable:affectable UL_Point_Virgule
				{:
					/* void method call */
					Expression callerObject = ((MethodAssignment) affectable).getCallerObject();
					String name = ((MethodAssignment) affectable).getName();
					List<Expression> parameters = ((MethodAssignment) affectable).getParameters();
					RESULT = new VoidMethod(callerObject, name, parameters);
				:}
			/* fin ajout miniJava */
			| UL_Afficher Expression:expression UL_Point_Virgule
				{:
					RESULT = new Printer( expression );
				:}
			| UL_Si UL_Parenthese_Ouvrante Expression:condition UL_Parenthese_Fermante Block:alors UL_Sinon Block:sinon
				{: 
					RESULT = new Conditional( condition, alors, sinon ); 
				:}
			| UL_Si UL_Parenthese_Ouvrante Expression:condition UL_Parenthese_Fermante Block:alors 
				{: 
					RESULT = new Conditional( condition, alors, null); 
				:}
			| UL_Tant_Que UL_Parenthese_Ouvrante Expression:condition UL_Parenthese_Fermante Block:corps 
				{: 
					RESULT = new Repetition( condition, corps); 
				:}
			| UL_Retour Expression:expression UL_Point_Virgule
				{:
					RESULT = new Return( expression );
				:}
			;
			
Expressions ::= Expressions:expressions UL_Virgule Expression:expression 	
				{:
					expressions.add( expression );
					RESULT = expressions; 
				:}
            | Expression:expression								
            		{: 
					List<Expression> _expressions = new LinkedList<Expression>();
					_expressions.add( expression );
					RESULT = _expressions;
				:}
            ;
            
Affectable ::= UL_Identificateur:nom
				{:
					RESULT = new VariableAssignment( nom );
				:}
			/* début ajout miniJava */
			| UL_This
				{:
					RESULT = new This();
				:}
			| UL_Super
				{:
				:}
			/* fin ajout miniJava */
			| Affectable:tableau UL_Crochet_Ouvrant Expression:indice UL_Crochet_Fermant
				{:
					RESULT = new ArrayAssignment( tableau, indice);
				:}
			| UL_Parenthese_Ouvrante Affectable:affectable UL_Parenthese_Fermante
				{:
					RESULT = affectable;
				:}
			| UL_Parenthese_Ouvrante UL_Identificateur:type UL_Parenthese_Fermante Affectable:affectable    
             	{: 
					RESULT = new AssignableConversion( affectable, type); 
				:}
			| UL_Parenthese_Ouvrante Atomique:type UL_Parenthese_Fermante Affectable:affectable    
             	{: 
					RESULT = new AssignableConversion( affectable, type); 
				:}
			| Affectable:enregistrement UL_Point UL_Identificateur:etiquette
				{:
					RESULT = new FieldAssignment( enregistrement, etiquette);
				:}
			/* ajout miniJava : appels de méthode */
			| Affectable:affectable UL_Parenthese_Ouvrante UL_Parenthese_Fermante
				{:
				:}
			| Affectable:affectable UL_Parenthese_Ouvrante Expressions:parametres UL_Parenthese_Fermante
				{:
				:}
			/* ajout miniJava */
			;
			            
Expression ::= /* TODO (modifications à faire pour 2022) : Affectable:affectable UL_Egal Expression:expression 
				{:
					RESULT = new Assignment( affectable, expression);
				:}
			| */ Expression:gauche UL_Double_Egal Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Equals, droite); 
				:}
			| Expression:gauche UL_Exclamation_Egal Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Different, droite); 
				:}
			| Expression:gauche UL_Inferieur Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Lesser, droite); 
				:}
			| Expression:gauche UL_Superieur Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Greater, droite); 
				:}
			| Expression:gauche UL_Inferieur_Egal Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.LesserOrEqual, droite); 
				:}
			| Expression:gauche UL_Superieur_Egal Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.GreaterOrEqual, droite); 
				:}
			| Expression:gauche UL_Double_Barre Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Or, droite); 
				:}
            | Expression:gauche UL_Double_Esperluette Expression:droite        	
             	{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.And, droite); 
				:}
			| Expression:gauche UL_Plus Expression:droite         
				{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Add, droite); 
				:}
            | Expression:gauche UL_Moins Expression:droite        	
             	{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Substract, droite); 
				:}
            | Expression:gauche UL_Asterisque Expression:droite        
             	{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Multiply, droite); 
				:}
			| Expression:gauche UL_Oblique Expression:droite        
             	{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Divide, droite); 
				:}
			| Expression:gauche UL_Pour_Cent Expression:droite        
             	{: 
					RESULT = new BinaryExpression( gauche, BinaryOperator.Modulo, droite); 
				:}
            | UL_Moins Expression:expression                 
             	{: 
					RESULT = new UnaryExpression( UnaryOperator.Opposite, expression);
				:} 	
            | UL_Point_Exclamation Expression:expression                 
             	{: 
					RESULT = new UnaryExpression( UnaryOperator.Negate, expression); 
				:}
			| Expression:expression UL_Crochet_Ouvrant Expression:indice UL_Crochet_Fermant
				{:
					RESULT = new ArrayAccess( expression, indice );
				:}
			| Expression:expression UL_Point UL_Identificateur:etiquette
				{:
					RESULT = new FieldAccess( expression, etiquette );
				:}
			| Expression:condition UL_Point_Interrogation Expression:alors UL_Deux_Points Expression:sinon
				{:
					RESULT = new ConditionalExpression( condition, alors, sinon);
				:}
			| UL_Parenthese_Ouvrante Expression:expression UL_Parenthese_Fermante	         
             	{: 
					RESULT = expression; 
				:}
			| UL_Parenthese_Ouvrante UL_Identificateur:type UL_Parenthese_Fermante Expression:expression    
             	{: 
					RESULT = new AccessibleConversion( expression, type); 
				:}
			| UL_Parenthese_Ouvrante Atomique:type UL_Parenthese_Fermante Expression:expression    
             	{: 
					RESULT = new AccessibleConversion( expression, type); 
				:}
			| UL_Identificateur:nom	                     
             	{: 
					RESULT = new IdentifierAccess( nom ); 
				:}
			| /* début adaptation miniJava */
				Expression:fonction	UL_Parenthese_Ouvrante Expressions:parametres UL_Parenthese_Fermante                     
             	{: 
					/* to handle */
					Expression record = ((AbstractField) fonction).getRecord();
					String name = ((AbstractField) fonction).getName();
					RESULT = new MethodAccess(record, name, parametres);
				:}
			|
				Expression:fonction	UL_Parenthese_Ouvrante UL_Parenthese_Fermante                     
             	{:
					/* to handle */
					Expression record = ((AbstractField) fonction).getRecord();
					String name = ((AbstractField) fonction).getName();
					RESULT = new MethodAccess(record, name, null); 
				:}
				/* fin adaptation miniJava */
			| UL_Nombre_Entier:entier	                     
             	{: 
					RESULT = new IntegerValue( entier );
				:}
			| UL_Vrai                     
             	{: 
					RESULT = BooleanValue.True; 
				:}
			| UL_Faux                     
             	{: 
					RESULT = BooleanValue.False;
				:}
			/* début ajout miniJava */
			| UL_This
				{:
					RESULT = new IdentifierAccess( "this" ); 
				:}
			/* pas d'heritage en miniJava */
			| UL_Super
				{:
				:}
			/* fin ajout miniJava */
			| UL_Nul	                     
             	{: 
					RESULT = NullValue.Null; 
				:}
			| UL_Nombre_Flottant:flottant	                     
             	{: 
					RESULT = new FloatingValue( flottant ); 
				:}
			| UL_Caractere:caractere	                     
             	{: 
					RESULT = new CharacterValue( caractere );
				:}
			| UL_Chaine:chaine	                     
             	{: 
					RESULT = new StringValue( chaine ); 
				:}
			| UL_Nouveau Type:type UL_Crochet_Ouvrant Expression:taille UL_Crochet_Fermant	                     
             	{: 
					RESULT = new ArrayAllocation( type, taille ); 
				:}
			/* début adaptation miniJava : constructeur sans paramètre */
			| UL_Nouveau Type:type UL_Parenthese_Ouvrante UL_Parenthese_Fermante	                     
             	{: 
					RESULT = new ConstructorCall( type, null );
				:}
			/* fin adaptation miniJava */
			/* début ajout miniJava : constructeur avec paramètres */
			| UL_Nouveau Type:type UL_Parenthese_Ouvrante Expressions:parametres UL_Parenthese_Fermante	                     
             	{: 
					RESULT = new ConstructorCall( type, parametres );
				:}
			/* fin ajout miniJava */
            ;